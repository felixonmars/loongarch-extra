# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=1.0.63
pkgrel=2
pkgdesc='DDE network library and plugins'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('GPL3')
depends=(deepin-qt-dbus-factory gsettings-qt networkmanager-qt)
makedepends=(cmake deepin-control-center gtest ninja qt5-tools)
groups=(deepin)
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-core/archive/$pkgver.tar.gz"
        $pkgname-ldflags.patch::https://github.com/linuxdeepin/dde-network-core/pull/54.patch
        dde-network-core-fix-build.patch)
sha512sums=('b792e2352623ee0b9d5ccbc0979f7cf7d4275bc8e8193ccdd1ea194d721ce0d7f9525e5b517e9021df5691b7b8b36f909595abf88936ced2f4c34a621ecd8fbf'
            'caedd97f0da577edfec9462d14cfda790f434b996768f1b00bdca3d845eaec597e34441563f4412d3a0b83a7de4ae121cf5ba6c2c1a71c6ce2cbf6ea9965ed66'
            '5584fc427fefa3349eb74b403ac33ee0aff548a9326f8fcf2e1e80faf2ebaad5b92687ef4e8a7965de0567ad1736de707f6250a06f6dec0b7fa7ecc4a45c6d2b')

prepare() {
  cd dde-network-core-$pkgver
  patch -p1 -i ../$pkgname-ldflags.patch
  patch -p1 -i ../dde-network-core-fix-build.patch
}

build() {
  cd dde-network-core-$pkgver
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core-$pkgver
  DESTDIR="$pkgdir" ninja install
}
