# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-core
pkgver=2.0.15
pkgrel=3
pkgdesc='DDE network library and plugins'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-network-core"
license=('GPL3')
depends=(dtkcore dtkgui dtkwidget deepin-control-center glibc gcc-libs gsettings-qt
         networkmanager-qt5 qt5-base)
makedepends=(cmake gtest ninja qt5-tools)
groups=(deepin)
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-core/archive/$pkgver.tar.gz"
        dde-network-core-fix-build.patch)
sha512sums=('7ec7d0c4bc9af01d2c4b14410e940097cf08e4f850c674edafcf57046a3cbcfc63dfa2bd3976c40d3b2fe3438cb1ee87dfed8aaa9bd3e30d4ce65c3d61d4662e'
            '5584fc427fefa3349eb74b403ac33ee0aff548a9326f8fcf2e1e80faf2ebaad5b92687ef4e8a7965de0567ad1736de707f6250a06f6dec0b7fa7ecc4a45c6d2b')

prepare() {
  cd dde-network-core-$pkgver
  patch -p1 -i ../dde-network-core-fix-build.patch
}

build() {
  cd dde-network-core-$pkgver
  cmake -GNinja .
  ninja
}

package() {
  cd dde-network-core-$pkgver
  DESTDIR="$pkgdir" ninja install
}
