# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gnu-efi
pkgver=3.0.17
pkgrel=2
pkgdesc="Develop EFI applications using the GNU toolchain and the EFI development environment"
arch=(loong64 x86_64)
url="https://sourceforge.net/projects/gnu-efi/"
license=(BSD)
conflicts=(gnu-efi-libs)
provides=(gnu-efi-libs)
replaces=(gnu-efi-libs)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        "gnu-efi-3.0.15-la64.patch")
options=(!lto !strip)
sha512sums=('0893ca234272584f889b1ae1c75341a9ceee60acfd32765daa5d704191ba00450536a287b949304c6d055d1bf125cc29e24fc41df8e5230e0da4f9d944876512'
            '59fe71cdfc6e88a03dbaba1fa70dea6d7d091bf91af3c5a02ae2b40b122752f9a4e8242ebfc478202ede50e1bf38cf1d2e423e726c585f26fcdf3881f243e329')
b2sums=('27f8171b411a6a8a138d44d91c7e4e4291aa399562825d51a398913572119482ffeb303d7508ae13eacd2cd10b8f5098405ab16eb56243587efe93235f661285'
        '68541aff2e94d374999e30da10c8e81d0a77407ad0181085904a93f34ad34e161e1d33ee0e9ba3c67ddcd6f94da7e561dc2dca68663b295ee512533523c097e5')

prepare() {
  # -Werror, not even once
  sed -e 's/-Werror//g' -i $pkgname-$pkgver/Make.defaults
  cd $pkgname-$pkgver
  # insert LDFLFAGS into custom linker for apps
  patch -Np1 -i "../gnu-efi-3.0.15-la64.patch"
}

build() {
  cd $pkgname-$pkgver
  # NOTE: apply only minimal CFLAGS, as gnu-efi does not provide userspace
  # libs, but may be used in unitialized machine state and should therefore not
  # be architecture optmized
  CFLAGS="-O2"
  # upstream provides LDFLAGS directly to ld: https://sourceforge.net/p/gnu-efi/bugs/33/
  LDFLAGS="${LDFLAGS//-Wl/}"
  LDFLAGS="${LDFLAGS//,/ }"
  make
  make -C lib
  make -C gnuefi
  make -C inc
  make -C apps
}

package() {
  cd $pkgname-$pkgver
  make INSTALLROOT="$pkgdir" PREFIX=/usr install
  install -vDm 644 apps/*.efi -t "$pkgdir/usr/share/$pkgname/apps/$CARCH/"
  install -vDm 644 README.efilib -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {ChangeLog,README.{gnuefi,git,elilo}} -t "$pkgdir/usr/share/doc/$pkgname/"
}
