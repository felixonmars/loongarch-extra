# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-terminal
pkgver=1.27.1
pkgrel=1
pkgdesc="The MATE Terminal Emulator"
url="https://mate-desktop.org"
arch=('loong64' 'x86_64')
license=('GPL')
depends=('libsm' 'gettext' 'mate-desktop' 'vte3' 'perl')
makedepends=('itstool' 'python')
groups=('mate-extra')
conflicts=('mate-terminal-gtk3')
replaces=('mate-terminal-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
theme-colors-false.patch)
sha256sums=('8d6b16ff2cac930afce4625b1b8f30c055e314e5b3dae806ac5b80c809f08dbe'
            '08d9dc177c626970f7e4df487a4fcb41749d45a0ae8422d5529462a4bad33bd7')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i $srcdir/theme-colors-false.patch
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
