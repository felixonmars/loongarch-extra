# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=pkgstats
pkgver=3.2.10
pkgrel=1
pkgdesc='Submit a list of installed packages to the Arch Linux project'
url='https://pkgstats.archlinux.de/'
arch=('loong64' 'x86_64')
license=('GPL')
depends=('pacman' 'systemd')
makedepends=('git' 'go' 'just' 'gcc')
options=('!lto')
source=("${pkgname}::git+https://github.com/archlinux-de/pkgstats-cli.git?signed#tag=${pkgver}"
	pkgstats-la64.patch)
sha256sums=('SKIP'
            'f06c2cef51e8a7f56bf24b446b0d7f94144ecae55e2fb5b817d5f1d9e7d2d8a3')
validpgpkeys=('3E80CA1A8B89F69CBA57D98A76A5EF9054449A5C')

prepare() {
	cd "$pkgname"
	patch -p1 -i "$srcdir/$pkgname-la64.patch"
}

check() {
	cd "${srcdir}/${pkgname}"

	just test
}

build() {
	cd "${srcdir}/${pkgname}"

	just build
}

package() {
	cd "${srcdir}/${pkgname}"

	just install "${pkgdir}"
}
