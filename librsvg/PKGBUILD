# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=librsvg
pkgname=(librsvg librsvg-docs)
pkgver=2.55.1
pkgrel=1
epoch=2
pkgdesc="SVG rendering library"
url="https://wiki.gnome.org/Projects/LibRsvg"
arch=(loong64 x86_64)
license=(LGPL)
depends=(cairo freetype2 gdk-pixbuf2 glib2 harfbuzz libxml2 pango)
makedepends=(gobject-introspection vala git rust gi-docgen python-docutils)
checkdepends=(ttf-dejavu)
options=(debug)
_commit=b556c32bd6e57b3310ed0cea1ab77f48014a291b  # tags/2.55.1^0
source=("git+https://gitlab.gnome.org/GNOME/librsvg.git#commit=$_commit"
  librsvg-la64-build.patch)
sha256sums=('SKIP')

pkgver() {
  cd librsvg
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd librsvg
  patch -p1 -i $srcdir/librsvg-la64-build.patch
  NOCONFIGURE=1 ./autogen.sh
}

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

build() {
  cd librsvg
  find -name Cargo.lock -exec rm -f {} \;
  mkdir .cargo
  cat > .cargo/config.toml <<EOF
[source.crates-io]
registry = "https://gitee.com/yetist/crates.io-index"
EOF
  ./configure --prefix=/usr --disable-static --enable-vala --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

check() {
  # Test suite is very dependent on the versions of
  # Cairo, Pango, FreeType and HarfBuzz
  make -C librsvg check || :
}

package_librsvg() {
  provides=(librsvg-${pkgver%%.*}.so)

  make -C librsvg DESTDIR="$pkgdir" install

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_librsvg-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
sha256sums=('SKIP'
            '5ea1c5f71769e2275e8e8e64b5dc015160b609e73b11632a23dfa0237d454da7')
