# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=sbc
pkgver=2.0
pkgrel=1
pkgdesc="Bluetooth Subband Codec (SBC) library"
url="https://git.kernel.org/pub/scm/bluetooth/sbc.git"
arch=(loong64 x86_64)
license=(GPL LGPL)
depends=(glibc)
makedepends=(git)
provides=(libsbc.so)
options=(debug)
_commit=8dc5d5ba381512ad5b1afa45c63ec6b0a3833244  # tags/2.0^0
source=("git+$url#commit=$_commit"
        sbc-la64.patch)
b2sums=('SKIP'
        'e2bd76c7948fab7fdcb88effcafd27a85571e1d420887df480a46f203fe93dde46892e53ab842cc8f5f2737c4e9a14ee550b12e3a7947c2987f5be527c75dc56')

pkgver() {
  cd sbc
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd sbc
  patch -p1 -i $srcdir/sbc-la64.patch
  autoreconf -fvi
}

build() {
  cd sbc
  ./configure --prefix=/usr --disable-static --disable-tester
  make
}

check() {
  cd sbc
  make check
}

package() {
  cd sbc
  make DESTDIR="$pkgdir" install
}
