# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.6.0
pkgrel=2
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  e2fsprogs
  kbd
  pciutils
  procps-ng
  python
  python-pyparted
  python-simple-term-menu
  systemd
  util-linux
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-v$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-v$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
  archinstall-la64-2.6.0.patch
)
sha512sums=('4859e8dc2d723a153fba862f6c31f1018fe238e567fd3c64b02c8fd6a7b1d8e01ef29678a6b4cf8e3a36792b4d5d564e20faa56cdae6f52741c916aaf103017f'
            'SKIP'
            '7173ab59b1387e35095b712f01c784f2904d4ec1c70233ed038910f9ae2230de57de2b3b1c9ecdd0b2564ab338c66800763322155065d200e5eb12bab692d9b0')
b2sums=('c9749980d6298716349b22d9108b923f9c8f2ce25128fc6dae84e4fc3ed748f3945da23050dad298aef955caa63aaeca24ed346639988aca86ae3e41a7949cc2'
        'SKIP'
        '1741541d72cb3a09f5b15fafd53356673f3baf94cba47459a04a2e316ccdb62a6f69d1ef826f1c4ed0edb38fbc13a726b9b0d832d4723be5fd110bb92e2ff982')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A' # Anton Hvornum (Torxed) <anton@hvornum.se>
              '8AA2213C8464C82D879C8127D4B58E897A929F2E') # Anton Hvornum (Torxed) <torxed@archlinux.org>

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i $srcdir/archinstall-la64-2.6.0.patch
  msgfmt -o archinstall/locales/zh-CN/LC_MESSAGES/base.mo archinstall/locales/zh-CN/LC_MESSAGES/base.po
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
