# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.5.1
pkgrel=2
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(python systemd)
makedepends=(python-build python-installer python-flit-core python-setuptools python-sphinx python-wheel)
provides=(python-archinstall)
conflicts=(python-archinstall)
replaces=(python-archinstall)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
  archinstall-la64.patch
)
sha512sums=('1916d9fc80e4048ff791a38d4b88bdae798f52fff7dfe1e49a1728321a2f7618b9af31f6664a020969e3a98b4cc85e6da9eaa1dc138e4072b4a749b04049fc0e'
            'SKIP'
            '4556874d01f3df1930d1c699570ba9e87960f88a2e1c9a6bba44770907b3461dbe3a0c30ab10328556a0feb18ea3a225e71c0d90ae8696911ec083020c3b3692')
b2sums=('a577026c7ab00e7da732aa84c807013befb0141ed1699f0dbe5b6ec1d325092e0f7338ce796a0453fac0c60a28c79423f3cd74a79b4afa8ea4756552c9c36c41'
        'SKIP'
        '9f3b2d8e39933dde17d032b6bab3dcb81cb1bff0e8a78849b611e4bd3f3db8931e69c0a378906a9905f26d2aabfbcddae4f7df1bb803a8662a02785b88b367d5')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/archinstall-la64.patch
  # use real directories for examples and profiles, as symlinks do not work
  # with flit or setuptools PEP517 backends
  rm -fv $pkgname/{examples,profiles}
  mv -v examples profiles $pkgname/
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
