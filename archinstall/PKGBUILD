# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.5.3
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(python systemd)
makedepends=(python-build python-installer python-flit-core python-setuptools python-sphinx python-wheel)
provides=(python-archinstall)
conflicts=(python-archinstall)
replaces=(python-archinstall)
source=(
  $url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  $pkgname-v$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
  archinstall-la64-2.5.3.patch
)
sha512sums=('5ee9ed024ba9567e0dde3bdb85195bc5b22d37352b872216be10414ea85d3f53c7824b6a977d744658366734c150538d80717d8b459c182d02224b561c108cde'
            'SKIP'
            '8ec357c8677c458532580957db839803442a0bfa41a0c4ef86eed3fab95a65b2d4f2b74fa3efa927e72414b7827070ed116241506e5c893978c56fa6bf9affcd')
b2sums=('baf4dd861d37f8f4a2c8231daadcece7a734698bd572b5da91bd43c93f857474f42083bf7385957919550e1f85a9ce276fc2393ef0ddecddca676e3c0277b6b5'
        'SKIP'
        'a6a019f4dab8d8cf2ace0642b67b081696ef53f231682e99373fe96e7630d4cb6a8ba94259ad634f10f91efddddb916808d4f90b61c4bc80ea98494525a3f955')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/archinstall-la64-2.5.3.patch
  # use real directories for examples and profiles, as symlinks do not work
  # with flit or setuptools PEP517 backends
  rm -fv $pkgname/{examples,profiles}
  mv -v examples profiles $pkgname/
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
