# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.6.0
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  e2fsprogs
  kbd
  pciutils
  procps-ng
  python
  python-pyparted
  python-simple-term-menu
  systemd
  util-linux
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-v$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-v$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
  archinstall-la64-2.5.6.patch
)
sha512sums=('4859e8dc2d723a153fba862f6c31f1018fe238e567fd3c64b02c8fd6a7b1d8e01ef29678a6b4cf8e3a36792b4d5d564e20faa56cdae6f52741c916aaf103017f'
            'SKIP'
            '38aba00f30fa1a402a05ada9e64dc036aefc840d4086e529f8948bfb3ff35382e85d9bf9882b0bc5f627fdac6b1faf521969285b7dfdb3438bd8ec45304fc6d2')
b2sums=('c9749980d6298716349b22d9108b923f9c8f2ce25128fc6dae84e4fc3ed748f3945da23050dad298aef955caa63aaeca24ed346639988aca86ae3e41a7949cc2'
        'SKIP'
        '987e659032ac5a99730ac689e128531b565e74c4e229df35cea52f1a42e9fd916b4b6ff2bfc550e7c0771efac625cfeb15fa936a86d5eae0f49ea85a0a563d20')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A' # Anton Hvornum (Torxed) <anton@hvornum.se>
              '8AA2213C8464C82D879C8127D4B58E897A929F2E') # Anton Hvornum (Torxed) <torxed@archlinux.org>

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i $srcdir/archinstall-la64-2.5.6.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
