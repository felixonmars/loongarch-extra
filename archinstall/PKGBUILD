# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.5.0
pkgrel=2
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(python)
makedepends=(python-build python-installer python-flit python-setuptools python-sphinx python-wheel)
provides=(python-archinstall)
conflicts=(python-archinstall)
replaces=(python-archinstall)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
  archinstall-la64.patch
)
sha512sums=('9f083da273145a8c69cbf900b7292e1f4793095428c67308f4b63ef1f1c538586bcb22b7dc8a8410e595e254c43c888c95a3199b62d87bda570ecf1ce4b828d4'
            'SKIP'
            '0d093521e089002fdcc2902dbce51d7f1e8d2e5ffeb7c321d8d43a296ad1d8fc127048babb6bdb49f8daa3436e29a403554aa5c5ecb72040ecf2f5023c33648f')
b2sums=('728205be5d9dbbe808975c8b86005e5e110c17ff53c9f723287eae1c4f479b90d26227a3152f7bb2765b58121c24995b76c5cca955b1455a8c3446208d577506'
        'SKIP'
        '1302735a49ac07944c1f4380425780dbec3ad37fb20dc39de848c1bed6a8a20c786e292c9228ded882cb8184889914cb8911af14399304881b3a706808ab5ee1')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/archinstall-la64.patch
  # use real directories for examples and profiles, as symlinks do not work
  # with flit or setuptools PEP517 backends
  rm -fv $pkgname/{examples,profiles}
  mv -v examples profiles $pkgname/
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
