# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Anton Hvornum <torxed@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.6.0
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(
  arch-install-scripts
  btrfs-progs
  coreutils
  cryptsetup
  e2fsprogs
  kbd
  pciutils
  procps-ng
  python
  python-pyparted
  python-simple-term-menu
  systemd
  util-linux
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-sphinx
  python-wheel
)
optdepends=(
  'python-systemd: for systemd-journald support'
)
provides=(python-archinstall)
source=(
  $pkgname-v$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-v$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
  archinstall-la64-2.6.0.patch
)
sha512sums=('4859e8dc2d723a153fba862f6c31f1018fe238e567fd3c64b02c8fd6a7b1d8e01ef29678a6b4cf8e3a36792b4d5d564e20faa56cdae6f52741c916aaf103017f'
            'SKIP'
            '8fc7674103577f249ab098eb01ed21eef35a21446bd66fe354ae71c3d700e74993a1db0b35f71efa84dccdc42e9d8374ed80a81c6a04a7861901e9d9af52e050')
b2sums=('c9749980d6298716349b22d9108b923f9c8f2ce25128fc6dae84e4fc3ed748f3945da23050dad298aef955caa63aaeca24ed346639988aca86ae3e41a7949cc2'
        'SKIP'
        '5b8e5fa6e0add242d434543d067d8b5191e8d48d680d3ff97316fdae598cc7fdf3a2bc51fe59ae307393a744dfc2edf0f313fc401d0a9f461be557d89d50ac43')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A' # Anton Hvornum (Torxed) <anton@hvornum.se>
              '8AA2213C8464C82D879C8127D4B58E897A929F2E') # Anton Hvornum (Torxed) <torxed@archlinux.org>

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i $srcdir/archinstall-la64-2.6.0.patch
  msgfmt -o archinstall/locales/zh-CN/LC_MESSAGES/base.mo archinstall/locales/zh-CN/LC_MESSAGES/base.po
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
