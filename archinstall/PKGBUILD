# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.5.1
pkgrel=2
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=(any)
url="https://github.com/archlinux/archinstall"
license=(GPL3)
depends=(python systemd)
makedepends=(python-build python-installer python-flit-core python-setuptools python-sphinx python-wheel)
provides=(python-archinstall)
conflicts=(python-archinstall)
replaces=(python-archinstall)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.sig::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.sig
  archinstall-la64.patch
)
sha512sums=('1916d9fc80e4048ff791a38d4b88bdae798f52fff7dfe1e49a1728321a2f7618b9af31f6664a020969e3a98b4cc85e6da9eaa1dc138e4072b4a749b04049fc0e'
            'SKIP'
            '4c80cbe73b80a8c85411877fd949706670e660f832436dc262e14f8f2a8c929d13d3769306bfbbc9e31fae6b621c34d8b76d8ffaf87282e7ebf9291ceb171c64')
b2sums=('a577026c7ab00e7da732aa84c807013befb0141ed1699f0dbe5b6ec1d325092e0f7338ce796a0453fac0c60a28c79423f3cd74a79b4afa8ea4756552c9c36c41'
        'SKIP'
        '9ff57c1da6a37636828daf10b592a73a7536dbc25646bc31567a5b4a6a2311a7848c0cc67aa7e942d7e024cf4cdfc96521101b0109d07612868c1895d25d38aa')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/archinstall-la64.patch
  # use real directories for examples and profiles, as symlinks do not work
  # with flit or setuptools PEP517 backends
  rm -fv $pkgname/{examples,profiles}
  mv -v examples profiles $pkgname/
  (cd archinstall/locales; bash ./locales_generator.sh)
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
  PYTHONDONTWRITEBYTECODE=1 make man -C docs
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/_build/man/archinstall.1 -t "$pkgdir/usr/share/man/man1/"
}
