# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
pkgname=wv
pkgver=1.2.9
pkgrel=5
pkgdesc="MSWord library can load and parse Word 2000, 97, 95 and 6 file formats"
arch=('loong64' 'x86_64')
url="https://sourceforge.net/projects/wvware"
license=('GPL')
depends=(libgsf libpng sh)
source=(https://www.abisource.com/downloads/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz
https://gitweb.gentoo.org/repo/gentoo.git/plain/app-text/wv/files/wv-1.2.9-format-security.patch)
md5sums=('dbccf2e9f747e50c913b7e3d126b73f7')
sha1sums=('db4717a151742dbdb492318f104504a92075543a')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/wv-1.2.9-format-security.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --mandir=/usr/share/man --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
md5sums=('dbccf2e9f747e50c913b7e3d126b73f7'
         '3f16c9e4bab60c12fdb8b80f655d5fc8')
sha1sums=('db4717a151742dbdb492318f104504a92075543a'
          '6a0928ca38b6ee12f70374f55e77a6c0d761d0f6')
