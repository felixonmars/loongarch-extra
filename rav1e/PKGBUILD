# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Bermond <dbermond@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>

pkgname=rav1e
pkgver=0.5.1
pkgrel=2
pkgdesc='An AV1 encoder focused on speed and safety'
arch=(loong64 x86_64)
url=https://github.com/xiph/rav1e/
license=(BSD)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo-c
  git
  nasm
  rust
)
provides=(librav1e.so)
_tag=1aff20cd23f718866ae7460e866f8d60b41f4422
source=(
  git+https://github.com/xiph/rav1e.git#tag=${_tag}
  https://github.com/xiph/rav1e/releases/download/v${pkgver}/Cargo.lock
)
sha256sums=('SKIP'
            '6baeb4e72ee86dd4c3d6c9acfcfac6a2e58a580101eb181daced09199a136868')

pkgver() {
  cd rav1e
  git describe --tags | sed 's/^v//'
}

prepare() {
  find -name Cargo.lock -exec rm -f {} \;
  mkdir -p .cargo
  cat > .cargo/config.toml <<EOF
[source.crates-io]
registry = "https://gitee.com/yetist/crates.io-index"
EOF
  cargo fetch \
    --manifest-path rav1e/Cargo.toml
}

build() {
  cargo build \
    --release \
    --manifest-path rav1e/Cargo.toml
  cargo cbuild \
    --release \
    --prefix=/usr \
    --manifest-path rav1e/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path rav1e/Cargo.toml
}

package() {
  cd rav1e

  cargo install \
    --no-track \
    --path . \
    --root "${pkgdir}"/usr
  cargo cinstall \
    --release \
    --prefix /usr \
    --destdir "${pkgdir}"
  install -Dm 644 LICENSE PATENTS -t "${pkgdir}"/usr/share/licenses/rav1e/
}

# vim: ts=2 sw=2 et:
