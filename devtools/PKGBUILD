# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=devtools
pkgver=20230105
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=('bash' 'openssh' 'subversion' 'rsync' 'arch-install-scripts'
         'git' 'bzr' 'mercurial' 'diffutils' 'util-linux' 'awk')
makedepends=('asciidoc')
optdepends=('btrfs-progs: btrfs support')
source=(${url}/uploads/f0e6fd93923fd7b81f1574081d722c39/devtools-${pkgver}.tar.gz
        ${url}/uploads/7153224a91ad9785b4209f8426e260a4/devtools-${pkgver}.tar.gz.sig
        devtools-loong64-1012.patch)
validpgpkeys=(
  '4AA4767BBC9C4B1D18AE28B77F2D434B9741E8AC' # Pierre Schmitz <pierre@archlinux.org>
  '86CFFCA918CF3AF47147588051E8B148A9999C34' # Evangelos Foutras <foutrelis@archlinux.org>
  '8FC15A064950A99DD1BD14DD39E4B877E62EB915' # Sven-Hendrik Haase <svenstaro@archlinux.org>
  'A2FF3A36AAA56654109064AB19802F8B0D70FC30' # Jan Alexander Steffens (heftig) <heftig@archlinux.org>
  'B81B051F2D7FC867AAFF35A58DBD63B82072D77A' # SÃ©bastien Luttringer <seblu@archlinux.org>
  '6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD' # Allan McRae (Developer) <allan@archlinux.org>
  'E240B57E2C4630BA768E2F26FC1B547C8D8172C8' # Levente Polyak <anthraxx@archlinux.org>
)
sha256sums=('8ade36f4102ef73b9e4db0b6d7a2a8111583ce3a6f990dfbef061c050af5ef4a'
            'SKIP'
            'b8315e3314d30b0a2c8fae9866635ed3faf2a7ef2576cd59bbf0425720767b28')
b2sums=('448f44882617794ffb26a146fa92e2b2649d25791751d9b9c5d6872880d0889a02c95d0761979b5afdda9d05bb43d38509d13d8a99c8426def470ffe12e05d8d'
        'SKIP'
        'b98b4befc73414dca8fe50fadff38e2297cdd70cd28f6657d222449c015353c3216f91756d40fc3db164a50dda0f81ca06af06b3e6377a591cda3ca1316610a2')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i $srcdir/devtools-loong64-1012.patch
}

build() {
  cd ${pkgname}-${pkgver}
  make BUILDTOOLVER="${pkgver}-${pkgrel}-${arch}" PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
