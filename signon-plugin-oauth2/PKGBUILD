# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-plugin-oauth2
_pkgname=signon-oauth2
pkgver=0.25
pkgrel=1
pkgdesc='OAuth 2 plugin for signon'
arch=(loong64 x86_64)
url="https://gitlab.com/accounts-sso/signon-plugin-oauth2"
license=(LGPL)
depends=(signond)
makedepends=(qt5-xmlpatterns)
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/accounts-sso/signon-plugin-oauth2/-/archive/VERSION_$pkgver/signon-plugin-oauth2-VERSION_$pkgver.tar.gz")
sha256sums=('48a5bb61bf74fca458c870c1a27eedfe058b20f016c19a72aad2f5b151516447')

prepare() {
  cd signon-plugin-oauth2-VERSION_$pkgver*

  sed -i '/-Werror/d' common-project-config.pri

  # Do not install tests and example
  echo 'INSTALLS =' >>tests/tests.pro
  echo 'INSTALLS =' >>example/example.pro
}

build() {
  cd signon-plugin-oauth2-VERSION_$pkgver*

  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd signon-plugin-oauth2-VERSION_$pkgver*

  make INSTALL_ROOT="$pkgdir" install
}
