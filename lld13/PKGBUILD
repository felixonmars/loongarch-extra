# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lld13
pkgver=13.0.1
_rdate=20220505
_rdate1=20220517
pkgrel=1
pkgdesc="Linker from the LLVM project"
arch=('loong64' 'x86_64')
url="https://lld.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('llvm13-libs')
makedepends=('llvm13' 'cmake' 'ninja' 'python')
checkdepends=('llvm')
options=('!lto') # LTO causes a couple of test failures
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($_source_base/lld-$pkgver-$_rdate1.tar.xz
        $_source_base/llvm-$pkgver-$_rdate.tar.xz
        $_source_base/libunwind-$pkgver-$_rdate.tar.xz)
sha256sums=('d28e1caf1d54bb6c83a483fff1f137f7d83078adba34653ee5089fa643410f38'
            '5af8782456df8199da714978a9a0759e395d6f4756d48c20bf7e42ec5e5881b5'
            'a019117e6feea8411cc0dcbe081caddf20d2ab09523dfa4fd17d224610c3f27d')
validpgpkeys+=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>
validpgpkeys+=('474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
  # https://bugs.llvm.org/show_bug.cgi?id=49228
  mv libunwind{-$pkgver.src,}

  cd lld-$pkgver.src
  mkdir build
}

build() {
  cd lld-$pkgver.src/build

  local cmake_args=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr/lib/llvm13
    -DCMAKE_SKIP_RPATH=ON
    -DLLVM_CONFIG_PATH=/usr/lib/llvm13/bin/llvm-config
    -DLLVM_EXTERNAL_LIT=/usr/bin/lit
    -DLLVM_INCLUDE_TESTS=ON
    -DLLVM_LINK_LLVM_DYLIB=ON
    -DLLVM_MAIN_SRC_DIR="$srcdir/llvm-$pkgver.src"
    -DSPHINX_WARNINGS_AS_ERRORS=OFF
  )
  cmake .. "${cmake_args[@]}"
  ninja
}

check() {
  cd lld-$pkgver.src/build
  ninja check-lld
}

package() {
  cd lld-$pkgver.src/build

  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
