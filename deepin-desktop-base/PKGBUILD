# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-base
pkgver=2022.07.26
pkgrel=3
epoch=2
pkgdesc='Base component for Deepin'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-base"
license=('GPL3')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-base/archive/$pkgver.tar.gz"
        distribution.info
        deepin-deskto-base-la64.patch)
sha512sums=('ed9f3152e19f7c1de12b734f3db1698f7730d18663cdd6a91096d3b9d7fe17932f4efa163665468544db31de92e8757552b053b33e8f969c72e1e1192dfe2ee2'
            'd0609a457c14b57070a8baa179bd3f514f6e16e6d6452e106e7095b45f8fad6ce03411420926e96d2d8f76c2d71d82bc19f87f4e0033557dc6f762b502722bb4'
            'f79f5377f13e76ea812c822d9d6c41aa0faac16de1a0ddaa95b05673022fc5fe8f332f3bba6a99f6dfff4ffac6aa32d4bdd2ec44104fe1503de25cefa2709efe')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir/deepin-deskto-base-la64.patch"
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/distribution.info -t "$pkgdir"/usr/share/deepin/

  # Remove Deepin distro's lsb-release
  rm "$pkgdir"/etc/lsb-release

  # Don't override systemd timeouts
  rm -r "$pkgdir"/etc/systemd

  # Make a symlink for deepin-version
  ln -s ../usr/lib/deepin/desktop-version "$pkgdir"/etc/deepin-version

  # Install os-version and rename to uos-version
  cp files/os-version-amd files/os-version
  install -Dm644 files/os-version "$pkgdir"/etc/uos-version

  # Remove apt-specific templates
  rm -r "$pkgdir"/usr/share/python-apt
}
