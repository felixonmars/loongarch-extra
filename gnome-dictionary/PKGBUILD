# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-dictionary
pkgver=40.0
pkgrel=1
pkgdesc="Look up words in dictionary sources"
url="https://wiki.gnome.org/Apps/Dictionary"
arch=(loong64 x86_64)
license=(GPL2)
depends=(gtk3)
makedepends=(yelp-tools git meson appstream-glib)
_commit=c37edfc2cbf91d6bae6d5264d0dcfcb5dd34b313  # tags/40.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-dictionary.git#commit=$_commit"
    gnome-dictionary-fix-meson.patch)
sha256sums=('SKIP'
            '065c36c3e11d9f6482ebab26d71f1c0158f18ee880a0f116dfba94ace9e6bea3')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -p1 -i $srcdir/gnome-dictionary-fix-meson.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
