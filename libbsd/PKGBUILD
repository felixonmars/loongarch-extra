# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=libbsd
pkgver=0.11.6
pkgrel=2
pkgdesc='Provides useful functions commonly found on BSD systems like strlcpy()'
arch=('loong64' 'x86_64')
url="https://libbsd.freedesktop.org"
license=('custom')
depends=('glibc' 'libmd')
options=('staticlibs')
source=("https://libbsd.freedesktop.org/releases/$pkgname-$pkgver.tar.xz"{,.asc}
        libbsd-loongarch64.patch::https://gitlab.freedesktop.org/libbsd/libbsd/-/merge_requests/13/diffs.patch)
sha512sums=('9dbbfb84340fc69f59667241701d81d176439ce168f123344805898a269f7bd0e98abf8c7fc12d9bf539d1effb19424d93b647cc9120f693327e736d339e6075'
            'SKIP'
            '360564ccaaeeb8319eb05f9e66785ea3cc054a70bb30a179134ad49ce52308ec80b7f89af143a3c6122cdc60033e9a540e3d4e3066f89d50898dcdc3d3f24a81')
validpgpkeys=('4F3E74F436050C10F5696574B972BF3EA4AE57A3') # Guillem Jover

prepare() {
	cd "$pkgname-$pkgver"
	patch -p1 -i "$srcdir/libbsd-loongarch64.patch"
}

build() {
  cd "$pkgname-$pkgver"

  CFLAGS+=' -ffat-lto-objects' \
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  rm "${pkgdir}"/usr/lib/libbsd.a
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # included now in man-pages, FS#53351
  rm -f "${pkgdir}"/usr/share/man/man3/explicit_bzero.3
}
