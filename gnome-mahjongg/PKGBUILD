# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mahjongg
pkgver=3.40.0
pkgrel=1
pkgdesc="Disassemble a pile of tiles by removing matching pairs"
url="https://wiki.gnome.org/Apps/Mahjongg"
arch=(loong64 x86_64)
license=(GPL)
depends=(
  gtk4
  libadwaita
  librsvg
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
_commit=9bd0a49f97371d0cb1f2c0ecea94da407ed5c965  # tags/3.40.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-mahjongg.git#commit=$_commit"
        gnome-mahjongg-fix-meson.patch)
b2sums=('SKIP'
        '59dc0e8d4b2612dc7fa2d5f52729897f6c149c9bf7d57867d9a052775b4de160400805c432b445cc62b07e273f89f0c2cb6db6cd2c7d262c2b3fd76abfe99929')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -p1 -i $srcdir/gnome-mahjongg-fix-meson.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
