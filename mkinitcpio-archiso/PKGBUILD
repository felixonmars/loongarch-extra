# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mkinitcpio-archiso
pkgver=63
pkgrel=2
pkgdesc="Initcpio scripts used by archiso"
arch=(any)
url="https://gitlab.archlinux.org/mkinitcpio/mkinitcpio-archiso"
license=(GPL3)
depends=(bash device-mapper mkinitcpio)
makedepends=(git)
checkdepends=(shellcheck shfmt)
optdepends=(
  'curl: for PXE over HTTP'
  'mkinitcpio-nfs-utils: for PXE over NFS'
  'nbd: for PXE over NBD'
  'pv: for status display during copy to RAM'
)
source=("git+https://gitlab.archlinux.org/mkinitcpio/${pkgname}.git#tag=v${pkgver}?signed"
        mkinitcpio-archiso-loong64.patch)
sha256sums=('SKIP'
            '10efee93d9cd8e6769baaf7d6a6bc04e42f693d920977e986c8ab6dab3af1191')
validpgpkeys=(
  'C7E7849466FE2358343588377258734B41C31549'  # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF'  # nl6720 <nl6720@archlinux.org>
)

prepare() {
  cd $pkgname
  patch -p1 -i $srcdir/mkinitcpio-archiso-loong64.patch
}

check() {
  make -k check -C $pkgname
}

package() {
  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname
}
