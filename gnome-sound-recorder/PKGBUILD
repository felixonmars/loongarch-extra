# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sound-recorder
pkgver=42.0
pkgrel=1
pkgdesc="A utility to make simple audio recording from your GNOME desktop"
url="https://wiki.gnome.org/Apps/SoundRecorder"
arch=(any)
license=(GPL)
depends=(gtk4 gjs gst-plugins-good gst-plugins-base gst-plugins-bad-libs
         libadwaita)
makedepends=(gobject-introspection git meson)
groups=(gnome-extra)
_commit=0fabec18319c0baf0c627131c023f7c191af65d9  # tags/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-sound-recorder.git#commit=$_commit"
    gnome-sound-recorder-fix.patch)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -p1 -i $srcdir/gnome-sound-recorder-fix.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
sha256sums=('SKIP'
            'c8a14a886ecddb803eedb403dd8edc77ad311c23a42ad9631154adce0dcfbd56')
